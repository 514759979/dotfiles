#!/bin/zsh

(($#* >= 1)) || {
    echo Usage: $0 files
    return 1
}

for i ($*) {
    local use_file=0

    case $i {
        (*.zip|*.7z|*.jar|*.rar|*.iso)
        7z l $i
        ;;

        (*.tar|*.tgz|*.txz|*.tbz2|*.tar.*)
        tar -tavf $i
        ;;

        (*.gz)
        zcat $i
        ;;

        (*.xz)
        xzcat $i
        ;;

        (*.bz2)
        bzcat $i
        ;;

        (*.lzma)
        lzcat $i
        ;;

        (*.lz4)
        lz4cat $i
        ;;

        (*.ar)
        ar tv $i
        ;;

        (*)
        use_file=1
        ;;
    }

    if ((use_file)) {
        case $(file -bz $i) {
            (Zip *|7-zip *|RAR *)
            7z l $i
            ;;

            (POSIX tar *)
            tar -tavf $i
            ;;

            (*gzip *)
            zcat $i
            ;;

            (*bzip2 *)
            bzcat $i
            ;;

            (*XZ *)
            xzcat $i
            ;;

            (*LZMA *)
            lzcat $i
            ;;

            (*LZ4 *)
            lz4cat $i
            ;;

            (current ar archive)
            ar tv $i
            ;;
        }
    }
}
